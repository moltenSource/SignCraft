// Credits to OpenComputers
// https://github.com/MightyPirates/OpenComputers/blob/OC1.6-MC1.10/build.gradle

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.sc.version}"
group = "de.moltensource"
archivesBaseName = "SignCraft"

if (config.sc.subversion != null && config.sc.subversion != "")
    version += "-${config.sc.subversion}"

ext.simpleVersion = version
version = "MC${config.mc.version}-${project.version}"

minecraft {
    version = "${config.mc.version}-${config.forge.version}"

    replaceIn "de/moltensource/SignCraft.java"
    replace "@VERSION@", project.simpleVersion

    runDir = "run"
    mappings = "${config.mc.mappings}"
}

configurations {
    provided
    embedded
    compile.extendsFrom provided, embedded
}

dependencies {
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.simpleVersion, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea.module.scopes.PROVIDED.plus += [configurations.provided]

// this is needed for IntelliJ so we don't have to copy over the assets manually every time
idea {
    module {
        outputDir = file('build/classes/main')
    }
}